apply plugin: 'com.android.library'

import groovy.json.JsonSlurper

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 28)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('compileSdkVersion', 28)

        buildConfigField("String", "MODULE_VERSION", "\"${getModuleVersion()}\"")
    }
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def getModuleVersion() {
    def jsonFile = file('../package.json')
    def parsedJson = new JsonSlurper().parseText(jsonFile.text)

    return parsedJson["version"]
}

dependencies {
    implementation 'com.facebook.react:react-native:+'
    api 'com.iterable:iterableapi:3.4.13'
    // api project(':iterableapi') // links to local android SDK repo rather than by release
}
